(*****************************************************************************)
(* Open Source License                                                       *)
(* Copyright (c) 2021 Valentin Chaboche                                      *)
(* Copyright (c) 2021 Nomadic Labs, <contact@nomadic-labs.com>               *)
(*                                                                           *)
(* Permission is hereby granted, free of charge, to any person obtaining a   *)
(* copy of this software and associated documentation files (the "Software"),*)
(* to deal in the Software without restriction, including without limitation *)
(* the rights to use, copy, modify, merge, publish, distribute, sublicense,  *)
(* and/or sell copies of the Software, and to permit persons to whom the     *)
(* Software is furnished to do so, subject to the following conditions:      *)
(*                                                                           *)
(* The above copyright notice and this permission notice shall be included   *)
(* in all copies or substantial portions of the Software.                    *)
(*                                                                           *)
(* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR*)
(* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,  *)
(* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL   *)
(* THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER*)
(* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING   *)
(* FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER       *)
(* DEALINGS IN THE SOFTWARE.                                                 *)
(*                                                                           *)
(*****************************************************************************)

module Encode = struct
  let to_string = Marshal.to_string

  let from_string x = Marshal.from_string x 0
end

module Snapshot = struct
  type t = { name : string; applications : (string * string) list }
  [@@deriving show { with_path = false }]

  let name x = x.name

  let applications x = x.applications

  let build name applications = { name; applications }

  let read path =
    if Sys.file_exists path then
      let ic = open_in path in
      let x = Marshal.from_channel ic in
      let () = close_in ic in
      Some x
    else None

  let write path snapshot =
    let oc = open_out path in
    let () = Marshal.to_channel oc snapshot [] in
    close_out oc

  let rec decode_str_aux :
      type a b.
      (a, b) Spec.t -> (a, b) Interpreter.args * string -> string * string =
   fun spec (args, res) ->
    let open Interpreter in
    let open Spec in
    match (args, spec) with
    | (Cons (x, xs), Arrow ({ printer; _ }, ys)) ->
        let s = printer x in
        let (x, y) : string * string = decode_str_aux ys (xs, res) in
        (s ^ " " ^ x, y)
    | (Nil, Result printer) ->
        let x = Encode.from_string res in
        ("", printer x)
    | _ -> assert false

  let decode_str spec snap =
    let applications =
      snap.applications
      |> List.map (fun (x, y) ->
             let x = Encode.from_string x in
             decode_str_aux spec (x, y))
    in
    { snap with applications }
end
